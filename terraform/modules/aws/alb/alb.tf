# ========== ALB Resource ==========
resource "aws_alb" "this" {
  load_balancer_type = "application"
  name               = var.alb_name
  subnets            = var.alb_subnets_id
  security_groups    = var.alb_security_groups_id
  idle_timeout       = var.alb_idle_timeout
  internal           = false
  enable_http2       = true
}

# ========== ALB Listener for HTTPS ==========
resource "aws_alb_listener" "https" {
  count             = var.alb_enable_https ? 1 : 0
  load_balancer_arn = aws_alb.this.id
  port              = "443"
  protocol          = "HTTPS"
  ssl_policy        = "ELBSecurityPolicy-2016-08"  # Recommended SSL policy
  certificate_arn   = aws_acm_certificate.this.arn  # Use the SSL certificate ARN from ACM

  default_action {
    target_group_arn = var.alb_target_group_arn
    type             = "forward"
  }
  // to avoid changes generated by CodeDeploy changes
  lifecycle {
    ignore_changes = [default_action]
  }
}

# ========== ALB Listener for HTTP ==========
resource "aws_alb_listener" "http" {
  load_balancer_arn = aws_alb.this.id
  port              = "80"
  protocol          = "HTTP"

  default_action {
    type = "redirect"

    redirect {
      protocol   = "HTTPS"
      port       = "443"
      status_code = "HTTP_301"  # Permanent redirect to HTTPS
    }
  }

  // to avoid changes generated by CodeDeploy changes
  lifecycle {
    ignore_changes = [default_action]
  }

}

# ========== Store the ALB Listener ARNs in Parameter Store ==========
resource "aws_ssm_parameter" "alb_https_listener_arn" {
  name  = var.ssm_alb_https_listener_arn_key
  type  = "String"
  value = var.alb_enable_https ? aws_alb_listener.https[0].arn : null
  description = "ARN of HTTPS ALB Listener"
  overwrite   = true
}

resource "aws_ssm_parameter" "alb_listeners" {
  name  = var.ssm_alb_listeners_arns_key
  overwrite   = true
  type  = "String"
  value = join(",", compact([
    aws_alb_listener.http.arn,
    var.alb_enable_https ? aws_alb_listener.https[0].arn : null
  ]))
  description = "Comma-separated list of ALB Listener ARNs for HTTP and HTTPS"
}